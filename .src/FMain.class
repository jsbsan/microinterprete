' Gambas class file

Private listaComando As Comando[] 'lista de comandos, de las reglas
Private SelectorEstrategias As New SwitchStrategyCrearComando
Private listaElementosDibujar As New Comando[]

Public Sub Form_Open()

    Settings.Read(Me)
    ListBoxComandosDisponibles.List = SelectorEstrategias.listaComando()
    Me.Title = "InterPrete-CAD " & "version " & Application.version & " Autor: JsbSan 2020"
    DrawingArea1.Tracking = True 'para ver elmovimiento del raton si lo muevo por el area de dibujo

End

Public Sub ButtonEjecutar_Click()

    Dim codigo As String

    TextAreaConsola.text = ""
    'lista de todas las ordenes(de dibujo,y de instruciones bucle)
    listaComando = New Comando[]

    listaElementosDibujar = New Comando[]
    'leer el textarea y sacar las ordenes

    codigo = TextAreaComandos.text

    'antes de ejecutar guarda lo que va a ejecutar
    file.Save("/tmp/codigo.bak", codigo)

    analiza(codigo)

    DrawingArea1.Refresh()

End

Public Sub SimpleLinea(x1 As Integer, y1 As Integer, x2 As Integer, y2 As Integer)

    Paint.MoveTo(x1, y1)
    Paint.LineTo(x2, y2)

End

Public Sub DrawingArea1_Draw()

    Dim ctmp As Comando

    ' 'lista de comandos graficos a dibujar
    For Each ctmp In listaElementosDibujar
        ctmp.dibujar()
    Next

End

Public Sub analiza(codigo As String)

    Dim comandotmp As New Comando
    Dim palabras As New String[]
    Dim ltmp As String
    Dim lineasDatos As String[]
    Dim resto As String
    Dim NombreOrden As String

    lineasDatos = Split(codigo, "\n") 'todas las ordenes ocupan una sola linea

    For Each ltmp In lineasDatos
        palabras = Split(ltmp, ",")
        If palabras.count <> 0 Then
            NombreOrden = palabras[0]
            palabras.Delete(0)
            resto = ""
            resto = palabras.Join(",")
            comandotmp = New Comando
            comandoTmp = SelectorEstrategias.operar(NombreOrden, resto)
            If IsNull(comandotmp) Then
                'error nomando desconocido
            Else
                comandotmp.ejecutar()

                If comandotmp.tipo = "dibujo" Then listaElementosDibujar.add(comandotmp)
                If comandotmp.tipo = "consola" Then TextAreaConsola.text &= comandotmp.consola()
            Endif

        Endif
    Next

End

Public Sub ListBoxComandosDisponibles_DblClick()

    Dim ComandoElegido As String

    ComandoElegido = ListBoxComandosDisponibles.List[ListBoxComandosDisponibles.Index]

    TextAreaComandos.text &= ComandoElegido & ","
    TextAreaComandos.SetFocus()
    TextAreaComandos.Pos = Len(TextAreaComandos.text)

    TextAreaAyuda.text = SelectorEstrategias.ayuda(ComandoElegido)

End

Public Sub Form_Close()

    Settings.Write(Me)

End

Public Sub URLLabel1_Click()

    'Iconos dise√±ados por <a href="https://www.flaticon.es/autores/srip" title="srip">srip</a> from <a href="https://www.flaticon.es/" title="Flaticon"> www.flaticon.es</a>
End

Public Sub ButtonAbrir_Click()

    If Dialog.OpenFile() Then
        Return 'se cancelo la operacion
    Else
        TextAreaComandos.text = File.Load(Dialog.Path)
    Endif

End

Public Sub ButtonGuardar_Click()

    If Dialog.SaveFile() Then

        Return 'se cancelo la operacion
    Else
        File.Save(Dialog.Path, TextAreaComandos.text)
    Endif

End

Public Sub ButtonTutorial_Click()

    formAyuda.contenido = SelectorEstrategias.tutorial()
    formAyuda.show()

End

Public Sub ButtonRecuperaBak_Click()

    TextAreaComandos.text = File.Load("/tmp/codigo.bak")

End

Public Sub DrawingArea1_MouseMove()

    LabelCoordenadas.Text = "(" & Mouse.x & "," & Mouse.y & ")"

End

Public Sub DrawingArea1_MouseDown()

    LabelCoordenadas.Text = "(" & Mouse.x & "," & Mouse.y & ")"

End

Public Sub ButtonCopiaCoordenadas_Click()

    Clipboard.Copy(Replace(Replace(LabelCoordenadas.text, "(", ""), ")", ""))

End

Public Sub ButtonEjemplo1_Click()

    TextAreaComandos.text = File.Load("./ejemplo/ArbolesAleatorios.txt")

End

Public Sub ButtonEjemplo2_Click()

    TextAreaComandos.text = File.Load("./ejemplo/bicicleta.txt")

End
