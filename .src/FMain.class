' Gambas class file

Private listaComando As Comando[] 'lista de comandos, de las reglas
Private SelectorEstrategias As New SwitchStrategyCrearComando
Private listaElementosDibujar As New Comando[]
Private escala As Float = 1

Private TransladaX As Float = 0
Private Transladay As Float = 0

Private inicialX As Float = 0
Private inicialy As Float = 0

Public Sub Form_Open()

    Settings.Read(Me)
    ListBoxComandosDisponibles.List = SelectorEstrategias.listaComando()
    Me.Title = "InterPrete-CAD " & "version " & Application.version & " Autor: JsbSan 2020"
    DrawingArea1.Tracking = True 'para ver elmovimiento del raton si lo muevo por el area de dibujo
    LabelEscala.Text = "Escala: " & Format(escala, "0.0%")
    LabelTraslado.text = "Traslado (" & Str(TransladaX) & "," & Str(transladay) & ")"

End

Public Sub ButtonEjecutar_Click()

    Dim codigo As String

    TextAreaConsola.text = ""
    'lista de todas las ordenes(de dibujo,y de instruciones bucle)
    listaComando = New Comando[]

    listaElementosDibujar = New Comando[]
    'leer el textarea y sacar las ordenes

    codigo = TextAreaComandos.text

    'antes de ejecutar guarda lo que va a ejecutar
    file.Save("/tmp/codigo.bak", codigo)

    analiza(codigo)

    DrawingArea1.Refresh()

End

' Public Sub SimpleLinea(x1 As Integer, y1 As Integer, x2 As Integer, y2 As Integer)
'
'     Paint.MoveTo(x1, y1)
'     Paint.LineTo(x2, y2)
'
' End

Public Sub DrawingArea1_Draw()

    Dim ctmp As Comando

    Paint.Translate(TransladaX, TransladaY)
    Paint.Scale(escala, escala)

    ' 'lista de comandos graficos a dibujar
    For Each ctmp In listaElementosDibujar
        ctmp.dibujar()
    Next

End

Public Sub SVGRefresh()

    Dim ctmp As Comando
    Dim svgtmp As SvgImage

    If Exist("/tmp/cad.svg") Then Kill "/tmp/cad.svg" 'borrado si existe el fichero

    svgtmp = New SvgImage(4962, 3508)

    Paint.Begin(svgtmp)
    Paint.Translate(TransladaX, TransladaY)
    Paint.Scale(escala, escala)

    ' 'lista de comandos graficos a dibujar
    For Each ctmp In listaElementosDibujar
        ctmp.dibujar()
    Next
    Paint.End

    svgtmp.Save("/tmp/cad.svg")
    Desktop.Open("/tmp/cad.svg")

End

Public Sub ImagenResfresh()

    Dim ctmp As Comando
    Dim imgtmp As Image

    imgtmp = New Image(4962, 3508, Color.White)

    Paint.Begin(imgtmp)
    Paint.Translate(TransladaX, TransladaY)
    Paint.Scale(escala, escala)

    ' 'lista de comandos graficos a dibujar
    For Each ctmp In listaElementosDibujar
        ctmp.dibujar()
    Next
    Paint.End

    imgtmp.Save("/tmp/cad.png")
    Desktop.Open("/tmp/cad.png")

End

Public Sub analiza(codigo As String)

    Dim comandotmp As New Comando
    Dim palabras As New String[]
    Dim ltmp As String
    Dim lineasDatos As String[]
    Dim resto As String
    Dim NombreOrden As String

    lineasDatos = Split(codigo, "\n") 'todas las ordenes ocupan una sola linea

    For Each ltmp In lineasDatos
        palabras = Split(ltmp, ",")
        If palabras.count <> 0 Then
            NombreOrden = palabras[0]
            palabras.Delete(0)
            resto = ""
            resto = palabras.Join(",")
            comandotmp = New Comando
            comandoTmp = SelectorEstrategias.operar(NombreOrden, resto)
            If IsNull(comandotmp) Then
                'error nomando desconocido
            Else
                comandotmp.ejecutar()

                If comandotmp.tipo = "dibujo" Then listaElementosDibujar.add(comandotmp)
                If comandotmp.tipo = "consola" Then TextAreaConsola.text &= comandotmp.consola()
            Endif

        Endif
    Next

End

Public Sub ListBoxComandosDisponibles_DblClick()

    Dim ComandoElegido As String

    ComandoElegido = ListBoxComandosDisponibles.List[ListBoxComandosDisponibles.Index]

    TextAreaComandos.text &= ComandoElegido & ","
    TextAreaComandos.SetFocus()
    TextAreaComandos.Pos = Len(TextAreaComandos.text)

    TextAreaAyuda.text = SelectorEstrategias.ayuda(ComandoElegido)

End

Public Sub Form_Close()

    Settings.Write(Me)

End

Public Sub URLLabel1_Click()

    'Iconos dise√±ados por <a href="https://www.flaticon.es/autores/srip" title="srip">srip</a> from <a href="https://www.flaticon.es/" title="Flaticon"> www.flaticon.es</a>
End

Public Sub ButtonAbrir_Click()

End

Public Sub GuardarCodigo()

    If Dialog.SaveFile() Then

        Return 'se cancelo la operacion
    Else
        File.Save(Dialog.Path, TextAreaComandos.text)
    Endif

End

Public Sub ButtonTutorial_Click()

    formAyuda.contenido = SelectorEstrategias.tutorial()
    formAyuda.show()

End

Public Sub ButtonRecuperaBak_Click()

    TextAreaComandos.text = File.Load("/tmp/codigo.bak")

End

Public Sub DrawingArea1_MouseMove()

    LabelCoordenadas.Text = "(" & Mouse.x & "," & Mouse.y & ")"
    ButtonEjecutar_Click()

End

Public Sub DrawingArea1_MouseDown()

    inicialX = Mouse.X
    inicialy = Mouse.y

End

Public Sub DrawingArea1_MouseUp()

    TransladaX -= (inicialx - Mouse.X)
    Transladay -= (inicialy - Mouse.y)
    ButtonEjecutar_Click()

    LabelTraslado.text = "Translado (" & Str(TransladaX) & "," & Str(transladay) & ")"

End

Public Sub ButtonCopiaCoordenadas_Click()

    Clipboard.Copy(Replace(Replace(LabelCoordenadas.text, "(", ""), ")", ""))

End

Public Sub ButtonEjemplo1_Click()

    TextAreaComandos.text = File.Load("./ejemplo/ArbolesAleatorios.txt")

End

Public Sub ButtonEjemplo2_Click()

    TextAreaComandos.text = File.Load("./ejemplo/bicicleta.txt")

End

Public Sub ZoomReiniciar_Click()

    escala = 1
    LabelEscala.Text = "Escala: " & Format(escala, "0.0%")
    ButtonEjecutar_Click()

End

Public Sub ZoomReducir_Click()

    escala -= 0.05
    If escala < 0 Then escala = 0.10
    LabelEscala.Text = "Escala: " & Format(escala, "0.0%")
    ButtonEjecutar_Click()

End

Public Sub ZoomMas_Click()

    escala += 0.05

    Labelescala.Text = "Escala: " & Format(escala, "0.0%")
    ButtonEjecutar_Click()

End

Public Sub DrawingArea1_MouseWheel()

    If Mouse.delta < 0 Then
        ZoomReducir_Click()
    Else
        ZoomMas_Click()
    Endif

End

Public Sub MenuTranladoInicial_Click()

    TransladaX = 0
    Transladay = 0
    ButtonEjecutar_Click()

    LabelTraslado.text = "Translado (" & Str(TransladaX) & "," & Str(transladay) & ")"

End

Public Sub MenuColorFondo_Click()

    If Not Dialog.SelectColor() Then
        DrawingArea1.Background = Dialog.Color
    Endif

End

Public Sub ButtonGuadarImagen_Click()

    GuardarComoPNG()

End

Public Sub GuardarComoPNG()

    Dim codigo As String
    Dim escalaactual As Float
    Dim antiguaX As Float
    Dim antiguay As Float

    Me.mouse = Mouse.wait
    Wait 0.01
    TextAreaConsola.text = ""

    escalaactual = escala
    antiguax = TransladaX
    antiguay = Transladay

    escala = 1
    TransladaX = 0
    Transladay = 0

    'lista de todas las ordenes(de dibujo,y de instruciones bucle)
    listaComando = New Comando[]

    listaElementosDibujar = New Comando[]
    'leer el textarea y sacar las ordenes

    codigo = TextAreaComandos.text

    'antes de ejecutar guarda lo que va a ejecutar
    file.Save("/tmp/codigo.bak", codigo)

    analiza(codigo)
    Me.mouse = Mouse.Default
    Wait 0.01
    ImagenResfresh()

    escala = escalaactual
    TransladaX = antiguax
    Transladay = antiguay

End

Public Sub GuardarComoSVG()

    Dim codigo As String
    Dim escalaactual As Float
    Dim antiguaX As Float
    Dim antiguay As Float

    Me.mouse = Mouse.wait
    Wait 0.01
    TextAreaConsola.text = ""

    escalaactual = escala
    antiguax = TransladaX
    antiguay = Transladay

    escala = 1
    TransladaX = 0
    Transladay = 0

    'lista de todas las ordenes(de dibujo,y de instruciones bucle)
    listaComando = New Comando[]

    listaElementosDibujar = New Comando[]
    'leer el textarea y sacar las ordenes

    codigo = TextAreaComandos.text

    'antes de ejecutar guarda lo que va a ejecutar
    file.Save("/tmp/codigo.bak", codigo)

    analiza(codigo)
    Me.mouse = Mouse.Default
    Wait 0.01
    SVGRefresh()

    escala = escalaactual
    TransladaX = antiguax
    Transladay = antiguay

End

Public Sub ButtonEjemplo3_Click()

    TextAreaComandos.text = File.Load("./ejemplo/a3.txt")

End

Public Sub MenuGuardarcomoPNG_Click()

    GuardarComoPNG()

End

Public Sub MenuGuardarComoSVG_Click()

    GuardarComoSVG()

End

Public Sub MenuGuardarCodigo_Click()

    GuardarCodigo()

End

Public Sub MenuAbrirCodigo_Click()

    If Dialog.OpenFile() Then
        Return 'se cancelo la operacion
    Else
        TextAreaComandos.text = File.Load(Dialog.Path)
    Endif

End
